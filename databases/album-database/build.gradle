import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    dependencies {
        classpath "mysql:mysql-connector-java:$mysqlVersion"
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

apply from: "../db.gradle"

flyway {
    url = "jdbc:mysql://localhost:3306/albums?useSSL=false"
    user = "root"
    outOfOrder = false
}

def tunnelProcess

task openTunnel {
    doLast {
        println 'Opening Tunnel'
        Thread.start {
            tunnelProcess = "cf ssh -N -L 63306:${getMysqlHost("album-service")}:3306 album-service".execute()
        }

//        sleep 5_000L
        waitForSocketOnPort(63306)

    }
}

private def waitForSocketOnPort(int port) {
    def attempts = 0
    def isOpen = false

    while (!isOpen && attempts <= 200) {
        try {
            new Socket("localhost", port)
            isOpen = true
        } catch (IOException e) {
            attempts += 1
            Thread.sleep(200)
        }
    }

    if (!isOpen) {
        throw new GradleException("Timed out waiting for SSH tunnel to open")
    }
}

task closeTunnel {
    doLast {
        println 'Closing Tunnel'
        tunnelProcess.destroy()
    }
}

task cfMigrate(type: FlywayMigrateTask, dependsOn: openTunnel) {
    if (System.env.CF_MIGRATE) {
        extension = setupProdFlywayExtension(new FlywayExtension(), "album-service")
    }
}

cfMigrate.finalizedBy closeTunnel
